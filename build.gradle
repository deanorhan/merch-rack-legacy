plugins {
  id 'org.springframework.boot'
  id 'io.spring.dependency-management'
  id 'java'
  id 'jacoco'
  id 'org.liquibase.gradle' version '2.2.0'
  id 'io.freefair.lombok' version '8.0.1'
  id 'org.owasp.dependencycheck' version '8.2.1'
  id 'com.github.andygoossens.gradle-modernizer-plugin' version '1.7.0'
  id 'pmd'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'com.diffplug.spotless' version '6.18.0'
  // Fucking buildship
  // id 'eclipse'

  // Leaving this in even though it doesn't run on gradle 8
  id 'org.springdoc.openapi-gradle-plugin' version '1.6.0'
}

group = 'org.daemio'
version = '0.1.0'
sourceCompatibility = '17'

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
    }
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

compileJava {
  options.compilerArgs += "-Amapstruct.defaultComponentModel=spring"
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-web'

  implementation 'ch.qos.logback:logback-classic:1.4.6'
  implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
  implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
  implementation 'net.logstash.logback:logstash-logback-encoder:7.3'

  implementation 'org.mapstruct:mapstruct:1.5.3.Final'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

  annotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen:6.2.0.Final'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

  implementation 'io.micrometer:micrometer-registry-prometheus'
  implementation 'io.micrometer:micrometer-tracing-bridge-otel'

  implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
  implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
  implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

  // Bumping version due to CVE-2022-1471
  implementation 'org.yaml:snakeyaml:2.0'

  implementation 'org.liquibase:liquibase-core:4.20.0'
  implementation 'org.hibernate.orm:hibernate-community-dialects'
  runtimeOnly 'org.xerial:sqlite-jdbc:3.41.2.1'
  // runtimeOnly 'mysql:mysql-connector-java'
  // runtimeOnly 'org.postgresql:postgresql:42.6.0'

  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
  testImplementation 'org.junit.platform:junit-platform-suite:1.9.2'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'

  testImplementation 'net.datafaker:datafaker:1.8.1'

  testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.14.1'
  testImplementation 'com.jparams:to-string-verifier:1.4.8'

  testImplementation 'io.cucumber:cucumber-java:7.11.2'
  testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.11.2'
  testImplementation 'io.cucumber:cucumber-spring:7.11.2'

  testImplementation 'org.testcontainers:testcontainers:1.18.0'
  testImplementation 'org.testcontainers:junit-jupiter:1.18.0'
  testImplementation 'org.testcontainers:postgresql:1.18.0'

  integrationTestImplementation 'io.rest-assured:rest-assured:5.3.0'

  // Dependencies specific to the Liquibase life cycle
  liquibaseRuntime 'org.liquibase:liquibase-core:4.20.0'
  liquibaseRuntime 'info.picocli:picocli:4.7.1'
  liquibaseRuntime 'org.xerial:sqlite-jdbc:3.41.2.1'
}

task integrationTest(type: Test) {
  description = 'Runs integration tests.'
  group = 'verification'
  shouldRunAfter test

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  systemProperty "cucumber.plugin", "pretty, html:${buildDir}/reports/tests/cucumber/integration.html, json:${buildDir}/reports/tests/cucumber/integration.json, junit:${buildDir}/reports/tests/cucumber/integration.xml"
  systemProperty "app.security.jwt.secret-key", "mIAWni0nnXlQmCBOVZxwXi6+c4JKhzqJgXct8oBWX5B1mwZlVfqj/k6Z98XHrhtP"
  environment "SECRET_KEY", "mIAWni0nnXlQmCBOVZxwXi6+c4JKhzqJgXct8oBWX5B1mwZlVfqj/k6Z98XHrhtP"
}

tasks.withType(Test) {
  useJUnitPlatform()
}

jacocoTestReport {
  dependsOn tasks.withType(Test)
  executionData tasks.withType(Test)
  shouldRunAfter integrationTest

  reports {
    xml.required = true
  }

  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: [
        '**/*MapperImpl.class',
        '**/*_.class',
        '**/MerchServiceApplication.class'
      ])
    }))
  }
}

liquibase {
  activities {
    main {
      searchPath "${projectDir}/src/main/resources"

      changelogFile "db/changelog.yaml"
      url "jdbc:sqlite:sqlitesample.db"
      username "username"
      password "password"
    }
  }
}

pmd {
  sourceSets = [ sourceSets.main ]
}

dependencyUpdates {
  outputFormatter = "html"
}

spotless {
  java {
    removeUnusedImports()

    googleJavaFormat('1.16.0')

    importOrder('java|javax|jakarta', '', 'org.daemio', '\\#')
    indentWithSpaces(2)
    trimTrailingWhitespace()

    formatAnnotations()
  }

  format 'misc', {
    target '*.gradle', '*.md', '.gitignore'

    trimTrailingWhitespace()
    indentWithSpaces(2)
  }
}

check.dependsOn integrationTest
check.dependsOn jacocoTestReport

// This and the eclipse plugin inclusion are only here because buildship is shit
// eclipse {
// 	classpath {
// 		containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
// 		file.whenMerged { cp ->
// 			def entries = cp.entries;
// 			def src = new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main/', null)
// 			entries.add(src)
// 		}
// 	}
// }